type Anon = String;
action AnonAct;
@annotation("namespace")
namespace PhotoFlash {
  @annotation1("type")
  @annotation2("type")
  type LongAlias = Long;
  @annotation("action")
  action groupAction1;
  action groupAction2;
  @annotation("entity")
  entity User, User2 in UserGroup = {
    @annotation("attrDecl")
    "department": String,
    "jobLevel": Long,
  } tags String;
  entity UserGroup;
  entity Album in Album = {
    "account": Account,
    "private": Bool,
  };
  entity Account = {
    "admins"?: Set<User>,
    "owner": User,
  };
  entity Photo in Album = {
    "account": Account,
    "private": Bool,
  };
  entity PhotoFormat enum ["jpg", "gif", "png"];
  entity AccountType enum ["user"];
  action "uploadPhoto" in groupAction1 appliesTo {
    principal: User,
    resource: Album,
    context: {
      "authenticated": Bool,
      "photo": {
        "file_size": Long,
        "file_type": String,
      },
    }
  };
  type authenticatedContext = {
    "authenticated": Bool,
  };
  action "viewPhoto" in [groupAction1, groupAction2, random::nested::name::"actionGroup"] appliesTo {
    principal: User,
    resource: Photo,
    context: authenticatedContext,
  };
  action "listAlbums" appliesTo {
    principal: User,
    resource: Account,
    context: authenticatedContext,
  };
}